################################################################################
#
# RevBayes Example: Bayesian inference of conitinuous trait evolution under a
#                   state-dependent OU model
#
#
# authors: Priscilla Lau
#
################################################################################

#######################
# Reading in the Data #
#######################

### Select the character to analyze
characterCont <- 1
characterDisc <- 1

### Read in the tree
T <- readTrees("data/2_simulation/mammal_diet_height1_n500.tre")[1]
ntips <- T.ntips()
nbranches <- 2 * ntips - 2

### Read in the continuous-character data
cont <- readContinuousCharacterData("data/2_simulation/power_alpha/sim_" + SIM_INDEX + "/continuous.nex")
cont.excludeAll()
cont.includeCharacter(characterCont)

disc <- readDiscreteCharacterData("data/2_simulation/power_alpha/sim_" + SIM_INDEX + "/discrete.nex")
num_disc_states <- disc.getStateDescriptions().size()
disc.excludeAll()
disc.includeCharacter(characterDisc)


# Create some vector for the moves and monitors of this analysis
moves    = VectorMoves()
avmvn_move = mvAVMVN(weight=10, waitBeforeLearning=500, waitBeforeUsing=1000)
monitors = VectorMonitors()

##########################
# Specify the tree model #
##########################

tree <- T
root_age <- tree.rootAge()

########################################
# Specify the discrete-character model #
########################################

Q <- fnJC(num_disc_states)


# make the transition rate parameter
lambda ~ dnLognormal(ln(0.01), 0.4)
moves.append( mvScale(lambda, weight=1.0) )
avmvn_move.addVariable(lambda)

# make the data-augmented CTCM model
X ~ dnPhyloCTMCDASiteIID(tree, Q, branchRates=lambda, type="Standard", nSites=1)
X.clamp(disc)

# include proposals for the discrete character history
moves.append( mvCharacterHistory(ctmc=X, qmap_site=Q, graph="node",   proposal="rejection", weight=200.0) )
moves.append( mvCharacterHistory(ctmc=X, qmap_site=Q, graph="branch", proposal="rejection", weight=200.0) )



# keep track of the number of transitions
for(i in 1:nbranches) {
    num_changes[i] := sum(X.numCharacterChanges(i))
}
total_num_changes := sum(num_changes)

char_hist := X.characterHistories()


##########################
# Specify the rate model #
##########################

theta[1] ~ dnSoftBoundUniformNormal(-20, 20, 1, 0.95)
moves.append(mvSlide(theta[1], weight = 1.0) )
avmvn_move.addVariable(theta[1])


# 1.044495 is scaled such that q_0.975/q_0.025 = 60
# -0.94856035 is scaled such that q0.025 = 0.05 and q_0.975 = 3 (tree height = 1)
halflife[1] ~ dnLognormal(-0.94856035, 1.044495)
moves.append(mvScale(halflife[1], weight = 1.0) )
avmvn_move.addVariable(halflife[1])
alpha[1] := abs(ln(2)/halflife[1])
rho[1] := 1 - ( 1 - exp( -2 * alpha[1] * root_age ) ) / ( 2 * alpha[1] * root_age )

stv[1] ~ dnLognormal(ln(STV), 0.587405)
moves.append(mvScale(stv[1], weight = 1.0) )
avmvn_move.addVariable(stv[1])
sigma2[1] := stv[1] * 2 * alpha[1]


for (i in 2:num_disc_states){
  theta[i] ~ dnNormal(theta[i-1], 4)
  moves.append(mvSlide(theta[i], weight = 1.0) )
  avmvn_move.addVariable(theta[i])
  halflife[i] ~ dnLognormal(ln(halflife[i-1]), 1.044495)
  moves.append(mvScale(halflife[i], weight = 1.0) )
  avmvn_move.addVariable(halflife[i])
  alpha[i] := abs(ln(2)/halflife[i])
  rho[i] := 1 - ( 1 - exp( -2 * alpha[i] * root_age ) ) / ( 2 * alpha[i] * root_age )
  stv[i] ~ dnLognormal(ln(stv[i-1]), 0.587405)
  moves.append(mvScale(stv[i], weight = 1.0) )
  avmvn_move.addVariable(stv[i])
  sigma2[i] := stv[i] * 2 * alpha[i]
}

moves.append( avmvn_move )


alpha_compare := ifelse(alpha[1] > alpha[2], 1, 0)
halflife_compare := ifelse(halflife[1] > halflife[2], 1, 0)


##########################
# Specify the OU process #
##########################

Y ~ dnPhyloOUSD(char_hist, theta=theta, rootTreatment="optimum", alpha=alpha, sigma=sigma2^0.5)
Y.clamp(cont)

#############
# The Model #
#############

mymodel = model(Y)

### set up the monitors that will output parameter values to file and screen
monitors.append( mnModel(filename="output/2_simulation/power_alpha/sim_" + SIM_INDEX + "_run_" + RUN_INDEX + ".log", printgen=10) )
monitors.append( mnScreen(printgen=1000, theta) )
#monitors.append( mnFile( char_hist, filename="output/2_simulation/power_alpha/augch_trees/sim_" + SIM_INDEX + "_run_" + RUN_INDEX + ".trees", printgen=100 ) )

################
# The Analysis #
################

### workspace mcmc ###
mymcmc = mcmc(mymodel, monitors, moves, nruns=1, combine="none")

### run the MCMC ###
mymcmc.burnin(generations=5000, tuningInterval=10)
mymcmc.run(generations=50000)



## quit ##
q()

