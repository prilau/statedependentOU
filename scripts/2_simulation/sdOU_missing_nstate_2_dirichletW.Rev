################################################################################
#
# RevBayes Example: Testing missing for state-dependent OU model
#
# authors: Priscilla Lau
#
################################################################################

#######################
# Reading in the Data #
#######################

### Select the character to analyze
characterDisc <- 1
characterCont <- 1

### Read in the tree
T <- readTrees("data/1_validation/artiodactyla/artiodactyla.tree")[1]
ntips <- T.ntips()
nbranches <- 2 * ntips - 2

disc <- readDiscreteCharacterData("data/2_simulation/missing_state/sim_" + SIM_INDEX + "/nstate_" + NUM_MISSING + "_discrete.nex")

disc.excludeAll()
disc.includeCharacter(characterDisc)
num_disc_states <- disc.getStateDescriptions().size()

cont <- readContinuousCharacterData("data/2_simulation/missing_state/sim_" + SIM_INDEX + "/nstate_" + NUM_MISSING + "_continuous.nex")
cont.excludeAll()
cont.includeCharacter(characterCont)

# Create some vector for the moves and monitors of this analysis
moves    = VectorMoves()
monitors = VectorMonitors()
avmvn_rates = mvAVMVN(weight=20, waitBeforeLearning=500, waitBeforeUsing=1000)
avmvn_ou = mvAVMVN(weight=15, waitBeforeLearning=500, waitBeforeUsing=1000)

##########################
# Specify the tree model #
##########################

tree <- T
root_age <- tree.rootAge()

########################################
# Specify the discrete-character model #
########################################

# make the Q matrix

#rates_12 := 0.04380424
#rates_21 := 0.04380424

rates ~ dnDirichlet([1, 1]*5000)
moves.append( mvBetaSimplex( rates, weight=2 ) )
moves.append( mvDirichletSimplex( rates, weight=2 ) )
avmvn_rates.addVariable( rates )

rate_12 := rates[1]
rate_21 := rates[2]

Q := fnFreeK(rates, rescaled=TRUE)

lambda ~ dnLognormal(ln(0.1), 0.3)
moves.append( mvScale(lambda, weight=1.0) )
avmvn_rates.addVariable( lambda )
moves.append( avmvn_rates )

X ~ dnPhyloCTMCDASiteIID(tree, Q, branchRates=lambda, type="Standard", nSites=1)
X.clamp(disc)

# include proposals for the discrete character history
moves.append( mvCharacterHistory(ctmc=X, qmap_site=Q, graph="tip",    proposal="rejection", weight=400.0) )
moves.append( mvCharacterHistory(ctmc=X, qmap_site=Q, graph="node",   proposal="rejection", weight=400.0) )
moves.append( mvCharacterHistory(ctmc=X, qmap_site=Q, graph="branch", proposal="rejection", weight=400.0) )

# keep track of the number of transitions
for(i in 1:nbranches) {
    num_changes[i] := sum(X.numCharacterChanges(i))
}
total_num_changes := sum(num_changes)

char_hist := X.characterHistories()

##############################
# continuous character model #
##############################
theta[1] ~ dnUniform(-8, 8)
moves.append(mvSlide(theta[1], weight = 1.0) )
avmvn_ou.addVariable(theta[1])

halflife[1] ~ dnLognormal(2.1516, 0.587405)
moves.append(mvScale(halflife[1], weight = 1.0) )
avmvn_ou.addVariable(halflife[1])
alpha[1] := abs(ln(2)/halflife[1])

stv[1] ~ dnLognormal(ln(STV), 0.587405)
moves.append(mvScale(stv[1], weight = 1.0) )
avmvn_ou.addVariable(stv[1])
sigma2[1] := stv[1] * 2 * alpha[1]


# theta[2] always <= theta[1]
theta[2] ~ dnUniform(theta[1], 8)
moves.append(mvScale(theta[2], weight = 1.0) )
avmvn_ou.addVariable(theta[2])

halflife[2] ~ dnLognormal(ln(halflife[1]), 0.587405)
moves.append(mvScale(halflife[2], weight = 1.0) )
avmvn_ou.addVariable(halflife[2])
alpha[2] := abs(ln(2)/halflife[2])

stv[2] ~ dnLognormal(ln(stv[1]), 0.587405)
moves.append(mvScale(stv[2], weight = 1.0) )
avmvn_ou.addVariable(stv[2])
sigma2[2] := stv[2] * 2 * alpha[2]

moves.append( avmvn_ou )

##########################
# Specify the OU process #
##########################

Y ~ dnPhyloOUSD(char_hist, theta=theta, rootTreatment="optimum", alpha=alpha, sigma=sigma2^0.5)
Y.clamp(cont)

#############
# The Model #
#############

mymodel = model(Y)

monitors.append( mnModel(filename="output/2_simulation/missing_state/sim_" + SIM_INDEX + "/trace_nstate_" + NUM_MISSING + "_dirichlet_run_" + RUN_INDEX + ".log", printgen=10) )
monitors.append( mnScreen(printgen=1000) )
monitors.append( mnFile( char_hist, filename="output/2_simulation/missing_state/sim_" + SIM_INDEX + "/augch_nstate_" + NUM_MISSING + "_dirichlet_run_" + RUN_INDEX + ".trees", printgen=100 ) )




################
# The Analysis #
################

### workspace mcmc ###
mymcmc = mcmc(mymodel, monitors, moves, nruns=1, combine="none")


### run the MCMC ###
mymcmc.burnin(generations=5000, tuningInterval=100) # comment out after first run

#mymcmc.initializeFromCheckpoint("output/2_simulation/missing_state/sim_" + SIM_INDEX + "/nstate_" + NUM_MISSING + "_dirichlet_run_" + RUN_INDEX + ".state") #comment this out for the first run
mymcmc.run(generations=25000, tuningInterval=200, checkpointInterval=50, checkpointFile="output/2_simulation/missing_state/sim_" + SIM_INDEX + "/nstate_" + NUM_MISSING + "_dirichlet_run_" + RUN_INDEX + ".state")
mymcmc.operatorSummary()


## quit ##
q()