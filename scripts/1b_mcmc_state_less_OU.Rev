################################################################################
#
# RevBayes Example: Bayesian inference of rates of evolution under a
#                   state-dependent Brownian-motion model
#
#
# authors: Sebastian Hoehna
#
################################################################################

#######################
# Reading in the Data #
#######################

### Select the character to analyze
characterCont <- 1

### Read in the tree
T <- readTrees("data/3_empirical/haemulidae_Tree.tre")[1]
ntips <- T.ntips()
nbranches <- 2 * ntips - 2

### Read in the continuous-character data
cont <- readContinuousCharacterData("data/3_empirical/haemulidae_Continuous.nex")
cont.excludeAll()
cont.includeCharacter(characterCont)

# Create some vector for the moves and monitors of this analysis
moves    = VectorMoves()
monitors = VectorMonitors()

##########################
# Specify the tree model #
##########################

tree <- T
root_age <- tree.rootAge()



##########################
# Specify the rate model #
##########################

sigma2 ~ dnLoguniform(1e-3, 1)

moves.append(mvScale(sigma2, weight = 1.0) )

root_state ~ dnUniform(-10,10)

moves.append(mvSlide(root_state, weight = 1.0) )

##########################
# Specify the OU process #
##########################

Y ~ dnPhyloOrnsteinUhlenbeckREML(tree, sigma=sigma2^0.5, alpha=0.0, theta=root_state, rootStates=root_state)
Y.clamp(cont)

#############
# The Model #
#############

mymodel = model(Y)

### set up the monitors that will output parameter values to file and screen
monitors.append( mnModel(filename="output/state_less_BM/state_less_OU.log", printgen=10) )
monitors.append( mnScreen(printgen=1000, sigma2) )


################
# The Analysis #
################

### workspace mcmc ###
mymcmc = mcmc(mymodel, monitors, moves, nruns=2, combine="none")


### run the MCMC ###
mymcmc.burnin(generations=5000, tuningInterval=100)
mymcmc.run(generations=100000)



## quit ##
q()
