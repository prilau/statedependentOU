################################################################################
#
# RevBayes Example: Bayesian inference of rates of evolution under a
#                   constant-rate Brownian-motion model
#
#
# authors: Michael R. May and Sebastian HÃ¶hna
#
################################################################################

#######################
# Reading in the Data #
#######################

### Select the trait to analyze
character <- 1

### Read in the trees
T <- readTrees("data/haemulidaeTree.nex")[1]

### Read in the character data
cont <- readContinuousCharacterData("data/haemulidaeContinuous.nex")
cont.excludeAll()
cont.includeCharacter(character)

# Create some vector for the moves and monitors of this analysis
moves    = VectorMoves()
monitors = VectorMonitors()

##########################
# Specify the tree model #
##########################

tree <- T

##########################
# Specify the rate model #
##########################

sigma2 ~ dnLognormal(0.05787005, 0.587)
moves.append( mvScale(sigma2, weight=1.0) )


##########################
# Specify the BM process #
##########################

X ~ dnPhyloBrownianREML(tree, branchRates=sqrt(sigma2) )
X.clamp(cont)



#############
# The Model #
#############

mymodel = model(sigma2)

### set up the monitors that will output parameter values to file and screen
monitors.append( mnModel(filename="output/simple_BM_haemulidae.log", printgen=10) )
monitors.append( mnScreen(printgen=1000, sigma2) )





################
# The Analysis #
################

### workspace mcmc ###
mymcmc = mcmc(mymodel, monitors, moves, nruns=4, combine="mixed")


### run the MCMC ###
mymcmc.burnin(generations=500, tuningInterval=100)
mymcmc.run(generations=5000, tuningInterval=100)

## quit ##
q()
