################################################################################
#
# RevBayes Example: Bayesian inference of rates of evolution under a
#                   state-dependent Brownian-motion model
#
#
# authors: Sebastian Hoehna
#
################################################################################

#######################
# Reading in the Data #
#######################

### Select the character to analyze
characterDisc <- 3

### Read in the tree
T <- readTrees("data/3_empirical/mammal_perMY_n2955.tre")[1]
ntips <- T.ntips()
nbranches <- 2 * ntips - 2

disc <- readDiscreteCharacterData("data/3_empirical/mammal_diet_binary_Discrete.nex")
num_disc_states <- disc.getStateDescriptions().size()
disc.excludeAll()
disc.includeCharacter(characterDisc)


# Create some vector for the moves and monitors of this analysis
moves    = VectorMoves()
monitors = VectorMonitors()
avmvn_move = mvAVMVN(weight=10, waitBeforeLearning=500, waitBeforeUsing=1000)

##########################
# Specify the tree model #
##########################

tree <- T
root_age <- tree.rootAge()

########################################
# Specify the discrete-character model #
########################################

# make the Q matrix
rate_12 ~ dnLognormal(0,1)
rate_21 ~ dnLognormal(0,1)

avmvn_move.addVariable(rate_12)
avmvn_move.addVariable(rate_21)

moves.append( mvScale(rate_12, weight=1.0) )
moves.append( mvScale(rate_21, weight=1.0) )


rates := [rate_12, rate_21]

Q := fnFreeK(rates, rescaled=FALSE)

rf_prior <- rep(1,2)
rf ~ dnDirichlet( rf_prior )
moves.append( mvBetaSimplex( rf, weight=2 ) )
moves.append( mvDirichletSimplex( rf, weight=2 ) )


# make the data-augmented CTCM model
X ~ dnPhyloCTMC(tree=tree, Q=Q, rootFrequencies=rf, type="Standard")
X.clamp(disc)


#############
# The Model #
#############

mymodel = model(X)

monitors.append( mnModel(filename="output/3_empirical/ase_picky/trace.log", printgen=10) )
monitors.append( mnScreen(printgen=1000) )
monitors.append( mnJointConditionalAncestralState(tree=tree,
                                                  ctmc=X,
                                                  filename="output/3_empirical/ase_picky/states.log",
                                                  type="Standard",
                                                  printgen=10,
                                                  withTips=true,
                                                  withStartStates=false) )

monitors.append( mnStochasticCharacterMap(ctmc=X,
                                          filename="output/3_empirical/ase_picky/simmap.log",
                                          printgen=10,
                                          include_simmap=true) )





################
# The Analysis #
################

### workspace mcmc ###
mymcmc = mcmc(mymodel, monitors, moves, nruns=1, combine="none")


### run the MCMC ###
mymcmc.burnin(generations=5000, tuningInterval=100)
mymcmc.run(generations=20000, tuningInterval=200)

anc_states = readAncestralStateTrace("output/3_empirical/ase_picky/states.log")
anc_tree = ancestralStateTree(tree=tree, ancestral_state_trace_vector=anc_states, include_start_states=false, file="output/3_empirical/ase_picky/anc_states.tre", burnin=0.0, summary_statistic="MAP", site=1, nStates=2, reconstruction="marginal")
anc_states_stoch_map = readAncestralStateTrace("output/3_empirical/ase_picky/simmap.log")
char_map_tree = characterMapTree(tree=tree,
                 ancestral_state_trace_vector=anc_states_stoch_map,
                 character_file="output/3_empirical/ase_picky/marginal_character.tree",
                 posterior_file="output/3_empirical/ase_picky/marginal_posterior.tree",
                 burnin=0.0,
                 num_time_slices=500)


## quit ##
q()
