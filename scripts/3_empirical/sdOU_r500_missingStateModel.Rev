################################################################################
#
# RevBayes Example: Testing hidden rates for state-dependent OU model
#
# authors: Priscilla Lau
#
################################################################################

#######################
# Reading in the Data #
#######################

### Select the character to analyze
characterDisc <- 1
characterCont <- 1

### Read in the tree
T <- readTrees("data/3_empirical/mammal_perMY_r500.tre")[1]
ntips <- T.ntips()
nbranches <- 2 * ntips - 2

disc <- readDiscreteCharacterData("data/3_empirical/mammal_missing_r500_" + NUM_MISSING_STATE + "state_Discrete.nex")

disc.excludeAll()
disc.includeCharacter(characterDisc)
num_disc_states <- disc.getStateDescriptions().size()

cont <- readContinuousCharacterData("data/3_empirical/mammal_log_kg_r500_Continuous.nex")
cont.excludeAll()
cont.includeCharacter(characterCont)

# Create some vector for the moves and monitors of this analysis
moves    = VectorMoves()
monitors = VectorMonitors()
avmvn_move = mvAVMVN(weight=10, waitBeforeLearning=500, waitBeforeUsing=1000)

##########################
# Specify the tree model #
##########################

tree <- T
root_age <- tree.rootAge()

########################################
# Specify the discrete-character model #
########################################

# make the Q matrix
Q := fnJC(num_disc_states)

lambda ~ dnLognormal(ln(0.01), 0.4)
moves.append( mvScale(lambda, weight=1.0) )
avmvn_move.addVariable(lambda)

X ~ dnPhyloCTMCDASiteIID(tree, Q, branchRates=lambda, type="Standard", nSites=1)
X.clamp(disc)


# include proposals for the discrete character history
moves.append( mvCharacterHistory(ctmc=X, qmap_site=Q, graph="tip",    proposal="rejection", weight=20.0) )
moves.append( mvCharacterHistory(ctmc=X, qmap_site=Q, graph="node",   proposal="rejection", weight=20.0) )
moves.append( mvCharacterHistory(ctmc=X, qmap_site=Q, graph="branch", proposal="rejection", weight=20.0) )

# keep track of the number of transitions
for(i in 1:nbranches) {
    num_changes[i] := sum(X.numCharacterChanges(i))
}
total_num_changes := sum(num_changes)

char_hist := X.characterHistories()

##############################
# continuous character model #
##############################
theta[1] ~ dnSoftBoundUniformNormal(-6.4, 12, 1, 0.95)
moves.append(mvSlide(theta[1], weight = 1.0) )
avmvn_move.addVariable(theta[1])

dtheta[1] := abs(ln(1))

halflife[1] ~ dnLognormal(4.3613845, 1.044495)
moves.append(mvScale(halflife[1], weight = 1.0) )
avmvn_move.addVariable(halflife[1])
alpha[1] := abs(ln(2)/halflife[1])

stv[1] ~ dnLognormal(ln(13), 0.587405)
moves.append(mvScale(stv[1], weight = 1.0) )
avmvn_move.addVariable(stv[1])
sigma2[1] := stv[1] * 2 * alpha[1]

for (i in 2:num_disc_states){
  # theta[i] always > theta[i+1]
  dtheta[i] ~ dnLognormal(0.5, abs(ln(2)))
  moves.append(mvSlide(dtheta[i], weight = 1.0) )
  avmvn_move.addVariable(dtheta[i])
  theta[i] := theta[i-1] + abs(dtheta[i])
  halflife[i] ~ dnLognormal(ln(halflife[i-1]), 1.044495)
  moves.append(mvScale(halflife[i], weight = 1.0) )
  avmvn_move.addVariable(halflife[i])
  alpha[i] := abs(ln(2)/halflife[i])
  stv[i] ~ dnLognormal(ln(stv[i-1]), 0.587405)
  moves.append(mvScale(stv[i], weight = 1.0) )
  avmvn_move.addVariable(stv[i])
  sigma2[i] := stv[i] * 2 * alpha[i]
}



##########################
# Specify the OU process #
##########################

Y ~ dnPhyloOUSD(char_hist, theta=theta, rootTreatment="optimum", alpha=alpha, sigma=sigma2^0.5)
Y.clamp(cont)

#############
# The Model #
#############

mymodel = model(Y)

#monitors.append( mnModel(filename="output/3_empirical/sdOU_tipState_missing/" + NUM_MISSING_STATE + "state_run_" + RUN_INDEX + ".log", printgen=10) )
#monitors.append( mnScreen(printgen=1000, theta) )
monitors.append( mnFile( char_hist, filename="output/3_empirical/sdOU_tipState_missing/" + NUM_MISSING_STATE + "state_run_" + RUN_INDEX + ".trees", printgen=10 ) )




################
# The Analysis #
################

### workspace mcmc ###
mymcmc = mcmc(mymodel, monitors, moves, nruns=1, combine="none")


### run the MCMC ###
mymcmc.burnin(generations=1000, tuningInterval=100)
mymcmc.run(generations=10000, tuningInterval=200)

## quit ##
q()
