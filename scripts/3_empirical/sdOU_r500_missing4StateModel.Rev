################################################################################
#
# RevBayes Example: Testing missing for state-dependent OU model
#
# authors: Priscilla Lau
#
################################################################################

#######################
# Reading in the Data #
#######################

### Select the character to analyze
characterDisc <- 1
characterCont <- 1

### Read in the tree
T <- readTrees("data/3_empirical/mammal_perMY_r500.tre")[1]
ntips <- T.ntips()
nbranches <- 2 * ntips - 2

disc <- readDiscreteCharacterData("data/3_empirical/mammal_missing_r500_Discrete_tmp.nex")

disc.excludeAll()
disc.includeCharacter(characterDisc)
num_disc_states <- disc.getStateDescriptions().size()

cont <- readContinuousCharacterData("data/3_empirical/mammal_log_kg_r500_Continuous.nex")
cont.excludeAll()
cont.includeCharacter(characterCont)

# Create some vector for the moves and monitors of this analysis
moves    = VectorMoves()
monitors = VectorMonitors()
avmvn_rates = mvAVMVN(weight=30, waitBeforeLearning=500, waitBeforeUsing=1000)
avmvn_ou = mvAVMVN(weight=15, waitBeforeLearning=500, waitBeforeUsing=1000)

##########################
# Specify the tree model #
##########################

tree <- T
root_age <- tree.rootAge()

########################################
# Specify the discrete-character model #
########################################

# make the Q matrix
#rate_12 := 0.00384
#rate_21 := 0.0194
#rate_13 := 0
#rate_31 := 0
#rate_14 := 0
#rate_41 := 0
#rate_23 := 0.0101
#rate_32 := 0.00649
#rate_24 := 0
#rate_42 := 0
#rate_34 := 0.0259
#rate_43 := 0.00159

tol_rate <- 0.00384 + 0.0194 + 0.0101 + 0.00649 + 0.0259 + 0.00159

rel_12 := 0.00384 / tol_rate
rel_21 := 0.0194 / tol_rate
rel_23 := 0.0101 / tol_rate
rel_32 := 0.00649 / tol_rate
rel_34 := 0.0259 / tol_rate
rel_43 := 0.00159 / tol_rate

rates ~ dnDirichlet([rel_12, rel_21, rel_23, rel_32, rel_34, rel_43]*2500)
moves.append( mvBetaSimplex( rates, weight=2 ) )
moves.append( mvDirichletSimplex( rates, weight=2 ) )
avmvn_rates.addVariable( rates )

rate_12 := rates[1]
rate_21 := rates[2]
rate_23 := rates[3]
rate_32 := rates[4]
rate_34 := rates[5]
rate_43 := rates[6]

Q := fnFreeK([rates[1], 0, 0, rates[2], rates[3], 0, 0, rates[4], rates[5], 0, 0, rates[6]], rescaled=TRUE)

lambda ~ dnLognormal(ln(0.005), 0.4)
moves.append( mvScale(lambda, weight=1.0) )
avmvn_rates.addVariable(lambda)

moves.append( avmvn_rates )

X ~ dnPhyloCTMCDASiteIID(tree, Q, branchRates=lambda, type="Standard", nSites=1)
X.clamp(disc)



# include proposals for the discrete character history
moves.append( mvCharacterHistory(ctmc=X, qmap_site=Q, graph="tip",    proposal="rejection", weight=1000.0) )
moves.append( mvCharacterHistory(ctmc=X, qmap_site=Q, graph="node",   proposal="rejection", weight=2000.0) )
moves.append( mvCharacterHistory(ctmc=X, qmap_site=Q, graph="branch", proposal="rejection", weight=2000.0) )

# keep track of the number of transitions
for(i in 1:nbranches) {
    num_changes[i] := sum(X.numCharacterChanges(i))
}
total_num_changes := sum(num_changes)

char_hist := X.characterHistories()

##############################
# continuous character model #
##############################
theta[1] ~ dnUniform(10, 15)
moves.append(mvSlide(theta[1], weight = 1.0) )
avmvn_ou.addVariable(theta[1])

halflife[1] ~ dnLognormal(4.16, 0.587405)
moves.append(mvScale(halflife[1], weight = 1.0) )
avmvn_ou.addVariable(halflife[1])
alpha[1] := abs(ln(2)/halflife[1])

stv[1] ~ dnLognormal(ln(10), 0.587405)
moves.append(mvScale(stv[1], weight = 1.0) )
avmvn_ou.addVariable(stv[1])
sigma2[1] := stv[1] * 2 * alpha[1]

# theta[2] < theta[1]
theta[2] ~ dnUniform(-10, theta[1])
moves.append(mvSlide(theta[2], weight = 1.0) )
avmvn_ou.addVariable(theta[2])

halflife[2] ~ dnLognormal(ln(halflife[1]), 0.587405)
moves.append(mvScale(halflife[2], weight = 1.0) )
avmvn_ou.addVariable(halflife[2])
alpha[2] := abs(ln(2)/halflife[2])
  
stv[2] ~ dnLognormal(ln(stv[1]), 0.587405)
moves.append(mvScale(stv[2], weight = 1.0) )
avmvn_ou.addVariable(stv[2])
sigma2[2] := stv[2] * 2 * alpha[2]

# theta[3]
theta[3] ~ dnUniform(-10, theta[2])
moves.append(mvScale(theta[3], weight = 1.0) )
avmvn_ou.addVariable(theta[3])

halflife[3] ~ dnLognormal(ln(halflife[2]), 0.587405)
moves.append(mvScale(halflife[3], weight = 1.0) )
avmvn_ou.addVariable(halflife[3])
alpha[3] := abs(ln(2)/halflife[3])
  
stv[3] ~ dnLognormal(ln(stv[2]), 0.587405)
moves.append(mvScale(stv[3], weight = 1.0) )
avmvn_ou.addVariable(stv[3])
sigma2[3] := stv[3] * 2 * alpha[3]


# theta[4] < theta[3]
theta[4] ~ dnUniform(-10, theta[3])
moves.append(mvScale(theta[4], weight = 1.0) )
avmvn_ou.addVariable(theta[4])

halflife[4] ~ dnLognormal(ln(halflife[3]), 0.587405)
moves.append(mvScale(halflife[4], weight = 1.0) )
avmvn_ou.addVariable(halflife[4])
alpha[4] := abs(ln(2)/halflife[4])
  
stv[4] ~ dnLognormal(ln(stv[3]), 0.587405)
moves.append(mvScale(stv[4], weight = 1.0) )
avmvn_ou.addVariable(stv[4])
sigma2[4] := stv[4] * 2 * alpha[4]


moves.append( avmvn_ou )

##########################
# Specify the OU process #
##########################

Y ~ dnPhyloOUSD(char_hist, theta=theta, rootTreatment="optimum", alpha=alpha, sigma=sigma2^0.5)
Y.clamp(cont)

#############
# The Model #
#############

mymodel = model(Y)

monitors.append( mnModel(filename="output/3_empirical/sdOU_r500_missingStateModel/trace_nstate_4_run_" + RUN_INDEX + ".log", printgen=10) )
monitors.append( mnScreen(printgen=1000) )
monitors.append( mnFile( char_hist, filename="output/3_empirical/sdOU_r500_missingStateModel/augch_nstate_4_run_" + RUN_INDEX + ".trees", printgen=100 ) )




################
# The Analysis #
################

### workspace mcmc ###
mymcmc = mcmc(mymodel, monitors, moves, nruns=1, combine="none")


### run the MCMC ###
#mymcmc.burnin(generations=5000, tuningInterval=100) # comment out after first run

#mymcmc.initializeFromCheckpoint("output/3_empirical/sdOU_r500_missingStateModel/nstate_4_run_" + RUN_INDEX + ".state") #comment this out for the first run
mymcmc.run(generations=100000, tuningInterval=200, checkpointInterval=50, checkpointFile="output/3_empirical/sdOU_r500_missingStateModel/nstate_4_run_" + RUN_INDEX + ".state")
mymcmc.operatorSummary()


## quit ##
q()
