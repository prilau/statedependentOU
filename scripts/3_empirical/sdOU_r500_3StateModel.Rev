################################################################################
#
# RevBayes Example: Testing hidden rates for state-dependent OU model
#
# authors: Priscilla Lau
#
################################################################################

#######################
# Reading in the Data #
#######################

### Select the character to analyze
characterDisc <- 1
characterCont <- 1

### Read in the tree
T <- readTrees("data/3_empirical/mammal_perMY_r500.tre")[1]
ntips <- T.ntips()
nbranches <- 2 * ntips - 2

disc <- readDiscreteCharacterData("data/3_empirical/mammal_diet3_r500_Discrete.nex")

disc.excludeAll()
disc.includeCharacter(characterDisc)
num_disc_states <- disc.getStateDescriptions().size()

cont <- readContinuousCharacterData("data/3_empirical/mammal_log_kg_r500_Continuous.nex")
cont.excludeAll()
cont.includeCharacter(characterCont)

# Create some vector for the moves and monitors of this analysis
moves    = VectorMoves()
monitors = VectorMonitors()

##########################
# Specify the tree model #
##########################

tree <- T
root_age <- tree.rootAge()

########################################
# Specify the discrete-character model #
########################################

# make the Q matrix


rate_12 ~ dnExp(root_age / 10)
rate_21 ~ dnExp(root_age / 10)
rate_13 ~ dnExp(root_age / 10)
rate_31 ~ dnExp(root_age / 10)
rate_23 ~ dnExp(root_age / 10)
rate_32 ~ dnExp(root_age / 10)

moves.append( mvScale(rate_12, weight=1.0) )
moves.append( mvScale(rate_21, weight=1.0) )
moves.append( mvScale(rate_13, weight=1.0) )
moves.append( mvScale(rate_31, weight=1.0) )
moves.append( mvScale(rate_23, weight=1.0) )
moves.append( mvScale(rate_32, weight=1.0) )

avmvn_rate_12 = mvAVMVN(weight=2, waitBeforeLearning=500, waitBeforeUsing=1000)
avmvn_rate_21 = mvAVMVN(weight=2, waitBeforeLearning=500, waitBeforeUsing=1000)
avmvn_rate_13 = mvAVMVN(weight=2, waitBeforeLearning=500, waitBeforeUsing=1000)
avmvn_rate_31 = mvAVMVN(weight=2, waitBeforeLearning=500, waitBeforeUsing=1000)
avmvn_rate_23 = mvAVMVN(weight=2, waitBeforeLearning=500, waitBeforeUsing=1000)
avmvn_rate_32 = mvAVMVN(weight=2, waitBeforeLearning=500, waitBeforeUsing=1000)

avmvn_rate_12.addVariable(rate_12)
avmvn_rate_21.addVariable(rate_21)
avmvn_rate_13.addVariable(rate_13)
avmvn_rate_31.addVariable(rate_31)
avmvn_rate_23.addVariable(rate_23)
avmvn_rate_32.addVariable(rate_32)

moves.append( avmvn_rate_12 )
moves.append( avmvn_rate_21 )
moves.append( avmvn_rate_13 )
moves.append( avmvn_rate_31 )
moves.append( avmvn_rate_23 )
moves.append( avmvn_rate_32 )

rates := [rate_12, rate_13, rate_21, rate_23, rate_31, rate_32]
Q := fnFreeK(rates, rescaled=TRUE)

lambda ~ dnLognormal(ln(0.01), 0.4) # should check lambda with aug model alone
moves.append( mvScale(lambda, weight=1.0) )
avmvn_lambda = mvAVMVN(weight=2, waitBeforeLearning=500, waitBeforeUsing=1000)
avmvn_lambda.addVariable( lambda )
moves.append( avmvn_lambda )

X ~ dnPhyloCTMCDASiteIID(tree, Q, branchRates=lambda, type="Standard", nSites=1)
X.clamp(disc)


# include proposals for the discrete character history
moves.append( mvCharacterHistory(ctmc=X, qmap_site=Q, graph="node",   proposal="rejection", weight=100.0) )
moves.append( mvCharacterHistory(ctmc=X, qmap_site=Q, graph="branch", proposal="rejection", weight=100.0) )

# keep track of the number of transitions
for(i in 1:nbranches) {
    num_changes[i] := sum(X.numCharacterChanges(i))
}
total_num_changes := sum(num_changes)

char_hist := X.characterHistories()

##############################
# continuous character model #
##############################
theta[1] ~ dnSoftBoundUniformNormal(-6.4, 12, 1, 0.95)
moves.append(mvSlide(theta[1], weight = 1.0) )
avmvn_theta[1] = mvAVMVN(weight=2, waitBeforeLearning=500, waitBeforeUsing=1000)
avmvn_theta[1].addVariable(theta[1])

halflife[1] ~ dnLognormal(4.3613845, 1.044495)
moves.append(mvScale(halflife[1], weight = 1.0) )
avmvn_halflife[1] = mvAVMVN(weight=2, waitBeforeLearning=500, waitBeforeUsing=1000)
avmvn_halflife[1].addVariable(halflife[1])
alpha[1] := abs(ln(2)/halflife[1])

stv[1] ~ dnLognormal(ln(13), 0.587405)
moves.append(mvScale(stv[1], weight = 1.0) )
avmvn_stv[1] = mvAVMVN(weight=2, waitBeforeLearning=500, waitBeforeUsing=1000)
avmvn_stv[1].addVariable(stv[1])
sigma2[1] := stv[1] * 2 * alpha[1]

for (i in 2:num_disc_states){
  theta[i] ~ dnNormal(theta[i-1], 4)
  moves.append(mvSlide(theta[i], weight = 1.0) )
  avmvn_theta[i] = mvAVMVN(weight=2, waitBeforeLearning=500, waitBeforeUsing=1000)
  avmvn_theta.addVariable(theta[i])
  
  halflife[i] ~ dnLognormal(ln(halflife[i-1]), 1.044495)
  moves.append(mvScale(halflife[i], weight = 1.0) )
  avmvn_halflife[i] = mvAVMVN(weight=2, waitBeforeLearning=500, waitBeforeUsing=1000)
  avmvn_halflife[i].addVariable(halflife[i])
  alpha[i] := abs(ln(2)/halflife[i])

  stv[i] ~ dnLognormal(ln(stv[i-1]), 0.587405)
  moves.append(mvScale(stv[i], weight = 1.0) )
  avmvn_stv[i] = mvAVMVN(weight=2, waitBeforeLearning=500, waitBeforeUsing=1000)
  avmvn_stv[i].addVariable(stv[i])
  sigma2[i] := stv[i] * 2 * alpha[i]
}


##########################
# Specify the OU process #
##########################

Y ~ dnPhyloOUSD(char_hist, theta=theta, rootTreatment="optimum", alpha=alpha, sigma=sigma2^0.5)
Y.clamp(cont)

#############
# The Model #
#############

mymodel = model(Y)

monitors.append( mnModel(filename="output/3_empirical/sdOU_r500_3StateModel/trace_run_" + RUN_INDEX + ".log", printgen=10) )
monitors.append( mnScreen(printgen=1000) )
monitors.append( mnFile( char_hist, filename="output/3_empirical/sdOU_r500_3StateModel/augch_run_" + RUN_INDEX + ".trees", printgen=100 ) )


################
# The Analysis #
################

### workspace mcmc ###
mymcmc = mcmc(mymodel, monitors, moves, nruns=1, combine="none")


### run the MCMC ###
mymcmc.burnin(generations=5000, tuningInterval=100) # comment out after first run

#mymcmc.initializeFromCheckpoint("output/3_empirical/sdOU_r500_3StateModel/run_" + RUN_INDEX + ".state") #comment this out for the first run
mymcmc.run(generations=50000, tuningInterval=200, checkpointInterval=50, checkpointFile="output/3_empirical/sdOU_r500_3StateModel/run_" + RUN_INDEX + ".state")
mymcmc.operatorSummary()

## quit ##
q()
