################################################################################
#
# RevBayes Example: Testing hidden rates for state-dependent OU model
#
# authors: Priscilla Lau
#
################################################################################

#######################
# Reading in the Data #
#######################

### Select the character to analyze
characterDisc <- 1
characterCont <- 1

### Read in the tree
T <- readTrees("data/1_validation/dummy/dummy_r6.tre")[1]
ntips <- T.ntips()
nbranches <- 2 * ntips - 2

disc <- readDiscreteCharacterData("data/1_validation/dummy/dummy_r6_tipState_hidden_missing_Discrete.nex")
num_disc_states <- disc.getStateDescriptions().size()

num_hidden_states <- 2
disc_exp <- disc.expandCharacters( num_hidden_states )
disc_exp.excludeAll()
disc_exp.includeCharacter(characterDisc)

cont <- readContinuousCharacterData("data/1_validation/dummy/dummy_r6_Continuous.nex")
cont.excludeAll()
cont.includeCharacter(characterCont)

# Create some vector for the moves and monitors of this analysis
moves    = VectorMoves()
monitors = VectorMonitors()
avmvn_move = mvAVMVN(weight=10, waitBeforeLearning=500, waitBeforeUsing=1000)

##########################
# Specify the tree model #
##########################

tree <- T
root_age <- tree.rootAge()

########################################
# Specify the discrete-character model #
########################################

# make the Q matrix
for (i in 1:4) {
  for (j in 1:4) {
    rates[i][j] <- 0.0
  }
}

rates[1][2] = 1
rates[1][3] = 1
rates[2][1] = 1
rates[2][4] = 1
rates[3][1] = 1
rates[3][4] = 1
rates[4][2] = 1
rates[4][3] = 1

Q := fnFreeK(rates, rescaled=TRUE)

lambda ~ dnLognormal(ln(1), 0.4)
moves.append( mvScale(lambda, weight=1.0) )
avmvn_move.addVariable(lambda)

# make the data-augmented CTCM model
X ~ dnPhyloCTMCDASiteIID(tree, Q, branchRates=lambda, type="NaturalNumbers", nSites=1)
X.clamp(disc_exp)

# include proposals for the discrete character history
moves.append( mvCharacterHistory(ctmc=X, qmap_site=Q, graph="tip",    proposal="rejection", weight=10.0) )
moves.append( mvCharacterHistory(ctmc=X, qmap_site=Q, graph="node",   proposal="rejection", weight=10.0) )
moves.append( mvCharacterHistory(ctmc=X, qmap_site=Q, graph="branch", proposal="rejection", weight=10.0) )

# keep track of the number of transitions
for(i in 1:nbranches) {
    num_changes[i] := sum(X.numCharacterChanges(i))
}
total_num_changes := sum(num_changes)

char_hist := X.characterHistories()

##############################
# continuous character model #
##############################

# set up OU parameters


theta[1] ~ dnSoftBoundUniformNormal(-5, 5, 1, 0.95)
moves.append(mvSlide(theta[1], weight = 1.0) )
avmvn_move.addVariable(theta[1])

theta[2] ~ dnNormal(theta[1], 4)
moves.append(mvSlide(theta[2], weight = 1.0) )
avmvn_move.addVariable(theta[2])

theta_gain[1] ~ dnLognormal(0.5, ln(2))
moves.append(mvScale(theta_gain[1], weight = 1.0) )
avmvn_move.addVariable(theta_gain[1])

theta_gain[2] ~ dnLognormal(0.5, ln(2))
moves.append(mvScale(theta_gain[2], weight = 1.0) )
avmvn_move.addVariable(theta_gain[2])

theta[3] := theta[1] + theta_gain[1]
theta[4] := theta[2] + theta_gain[2]

halflife[1] ~ dnLognormal(-1.234, 1.044495)
moves.append(mvScale(halflife[1], weight = 1.0) )
avmvn_move.addVariable(halflife[1])
alpha[1] := abs(ln(2)/halflife[1])

halflife[2] ~ dnLognormal(ln(halflife[1]), 1.044495)
moves.append(mvScale(halflife[2], weight = 1.0) )
avmvn_move.addVariable(halflife[2])
alpha[2] := abs(ln(2)/halflife[2])

halflife[3] := halflife[1]
halflife[4] := halflife[2]
alpha[3] := alpha[1]
alpha[4] := alpha[2]

stv[1] ~ dnLognormal(ln(13), 0.587405)
moves.append(mvScale(stv[1], weight = 1.0) )
avmvn_move.addVariable(stv[1])
sigma2[1] := stv[1] * 2 * alpha[1]

stv[2] ~ dnLognormal(ln(stv[1]), 0.587405)
moves.append(mvScale(stv[2], weight = 1.0) )
avmvn_move.addVariable(stv[2])
sigma2[2] := stv[2] * 2 * alpha[2]

stv[3] := stv[1]
stv[4] := stv[2]
sigma2[3] := sigma2[1]
sigma2[4] := sigma2[2]


##########################
# Specify the OU process #
##########################

Y ~ dnPhyloOUSD(char_hist, theta=theta, rootTreatment="optimum", alpha=alpha, sigma=sigma2^0.5)
Y.clamp(cont)

#############
# The Model #
#############

mymodel = model(Y)

monitors.append( mnModel(filename="output/1_validation/sdOU_tipState_hidden_missing/run_" + RUN_INDEX + ".log", printgen=10) )
#monitors.append( mnScreen(printgen=1000, theta) )
monitors.append( mnFile( char_hist, filename="output/1_validation/sdOU_tipState_hidden_missing/run_" + RUN_INDEX + ".trees", printgen=10 ) )




################
# The Analysis #
################

### workspace mcmc ###
mymcmc = mcmc(mymodel, monitors, moves, nruns=1, combine="none")


### run the MCMC ###
mymcmc.burnin(generations=1000, tuningInterval=100)
mymcmc.run(generations= 10000, tuningInterval=200)

## quit ##
q()
